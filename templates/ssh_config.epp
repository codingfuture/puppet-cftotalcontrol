<%- |
    String $ssh_dir,
    String $ssh_config,
    String $ssh_idkey,
    Array[String] $node_order,
    Hash $node_cfauth,
    Hash $node_facts,
    Hash $pool_proxy,
    Optional[String] $control_scope
| -%>
StrictHostKeyChecking no

ConnectTimeout 10
ServerAliveCountMax 3
ServerAliveInterval 15

# It does not play well in parallel & ProxyCommand
#ControlMaster auto
#ControlPersist 3m
#ControlPath <%= $ssh_dir %>/%C

IdentityFile <%= $ssh_idkey %>

<% $node_order.each |$nodename| {
    $cfauth_params = $node_cfauth[$nodename]
    $loc = $node_facts[$nodename]['cf_location']
    $locpool = $node_facts[$nodename]['cf_location_pool']
    $ssh_port = any2array($cfauth_params['sshd_ports'])[0]
    $proxy_host = pick_default(
        $pool_proxy["${loc}/${locpool}"],
        $pool_proxy[$loc]
    )
-%>
Host <%= $nodename %>
    User <%= $cfauth_params['admin_user'] %>
    Port <%= $ssh_port %>
<% if is_string($proxy_host) and $proxy_host != '' { 
    if $control_scope { -%>
    ProxyCommand /usr/bin/ssh -W <%= $nodename %>:<%= $ssh_port %> -F <%= $ssh_config %> <%= $control_scope %>_proxy@<%= $proxy_host %>
<%  } else { -%>
    ProxyCommand /usr/bin/ssh -W <%= $nodename %>:<%= $ssh_port %> -F <%= $ssh_config %> <%= $proxy_host %>
<% } } -%>

<% } -%>
